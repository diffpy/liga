#!/u24/local/bin/zsh -f

########################################################################
# Short Title: MC parallel tempering script
#
# Comments: zsh wrapper for running mcfitdist with parallel tempering
#
# $Id$
########################################################################

setopt no_bgnice extended_glob numeric_glob_sort
PROGNAME=${0:t}

# defaults:
mcfitdist=( ${0:h}/mcfitdist*-fast(.xN) ${0:h}/mcfitdist*~*fast*(.xN) )
mcfitdist=${mcfitdist[1]}
kbt=( 0.1 0.01 0.001 )
maxcputime=120
randseed=1
distfile=
inistru=
outstru=

########################################################################
print_help()
{
    print -l \
      "usage: ${PROGNAME} [DISTFILE] [inistru] [par1=val1...]" \
      "run parallel-tempering mcfitdist fits using distances from DISTFILE." \
      "Arguments are passed to mcfitdist with the exception of special" \
      "parameters for parallel tempering:"
    print -aC2 \
      "  mcfitdist=EXEFILE" "[${mcfitdist}]" \
      "  kbt=CSV"           "[${(j:,:)kbt}] array of annealing temperatures" \
      "  maxcputime=double" "[${maxcputime}] maximum time at one temperature" \
      "  randseed=bool"     "[${randseed}] apply random seed"
}

########################################################################
parse_args()
{
    typeset -ga mcfitdist_args
    for a; do
        if [[ $a == ?*=* ]]; then
            local par=${a%%[=]*}
            local val=${a#${par}[=]}
            case $par in
            (mcfitdist|kbt|maxcputime|outstru)
                eval $a
                continue
                ;;
            (randseed)
                if [[ ${(L)val} == (0|n|no|false) ]]; then
                    randseed=
                else
                    randseed=1
                fi
                continue
                ;;
            (kbt)
                kbt=( ${(s:,:)val} )
                continue
                ;;
	    (distfile)
		eval $a
		;;
	    (inistru)
		eval $a
		continue
		;;
            esac
	elif [[ $a == (-h|--help) ]]; then
	    print_help
	    exit
	elif [[ -z $distfile ]]; then
	    distfile=$a
	else
	    inistru=$a
	    continue
        fi
        mcfitdist_args+=$a
    done
    if [[ -z $outstru ]]; then
	print -u2 'outstru not defined'
	exit 2
    fi
    mcpt_outstru=( ${outstru:h}/${PROGNAME}$$-kbt{1..${#kbt}} )
}

########################################################################
# main
########################################################################

parse_args "$@"

if [[ -n $inistru ]]; then
    <$inistru tee $mcpt_outstru >/dev/null || exit 1
fi

# do the initial run, thus make sure all mcpt_outstru files are created
integer tidx
for tidx in {1..${#kbt}};
do
    # build a full list of arguments
    all_args=(
	$mcfitdist_args
	${inistru}
	kbt=${kbt[tidx]}
	maxcputime=$maxcputime
	outstru=${mcpt_outstru[tidx]}
	${randseed:+"randseed=$RANDOM"}
    )
    $mcfitdist $all_args
    MCSTATUS=$?
    if [[ $MCSTATUS == 0 ]]; then
	cp ${mcpt_outstru[tidx]} $outstru
	rm ${mcpt_outstru}
	exit
    elif [[ $MCSTATUS != 1 ]]; then
	exit $MCSTATUS
    fi
done

# main loop
integer tround=0
while true;
do
    (( tround++ ))
    # perform parallel tempering
    dvar=( $( dvar.py $distfile $mcpt_outstru | sed 's/.*[[:blank:]]//' ) )
    integer min_idx=1
    float min_val=${dvar[1]}
    for i in {1..${#kbt}}; do
	if (( ${dvar[i]} < min_val )); then
	    min_val=${dvar[i]}
	    min_idx=$i
	fi
    done
    print "$tround BT ${dvar[$i]}"
    cp ${mcpt_outstru[$i]} $outstru
    # compare results at different temperature
    integer idxthi idxtlo
    for idxthi in {1..$(( ${#kbt}-1 ))};
    do
	(( idxtlo = idxthi + 1 ))
	if (( ${dvar[idxthi]} < ${dvar[idxtlo]} ));
	then
	    print "$tround PT $idxtlo ${dvar[idxtlo]} $idxthi ${dvar[idxthi]}"
	    cp ${mcpt_outstru[$idxthi]} ${mcpt_outstru[$idxtlo]}
	else
	    print "$tround PT $idxthi ${dvar[idxthi]} $idxtlo ${dvar[idxtlo]}"
	    cp ${mcpt_outstru[$idxtlo]} ${mcpt_outstru[$idxthi]}
	fi
    done
    # perform MC runs at fixed temperature
    for tidx in {1..${#kbt}};
    do
	# build a full list of arguments
	all_args=(
	    $mcfitdist_args
	    kbt=${kbt[tidx]}
	    maxcputime=$maxcputime
	    inistru=${mcpt_outstru[tidx]}
	    outstru=${mcpt_outstru[tidx]}
	)
	if [[ -n $randseed ]]; then
	    all_args=( $all_args seed=$RANDOM )
	fi
	$mcfitdist $all_args
	MCSTATUS=$?
	if [[ $MCSTATUS == 0 ]]; then
	    cp ${mcpt_outstru[tidx]} $outstru
	    rm ${mcpt_outstru}
	    exit
	elif [[ $MCSTATUS != 1 ]]; then
	    exit $MCSTATUS
	fi
    done
done

########################################################################
# Too see what people have been up to just run:
#   cvs log
########################################################################
