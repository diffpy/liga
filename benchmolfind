#!/u24/local/bin/zsh -f

setopt extendedglob

# setup:
: ${REPEATS:=50}
TIMEFMT="%U"
distance_files=(
    solids/tetrahedron.dst
    solids/octahedron.dst
    solids/cube.dst
    solids/icosahedron.dst
    solids/dodecahedron.dst
    solids/prism12.dst
    solids/prismL16.dst
    solids/prism18.dst
#   solids/grid27.dst
)

if [[ $# == 0 || -n "${(M)*:#--help}" ]]; then
    print "usage: $0:t CmdToBench [SEED] DISTFILE arg1 arg2 ..."
    print "where SEED will be replaced with RANDOM integer and"
    print "DISTFILE will be replaced with following distance table files"
    print -c '  '${^distance_files}
    exit
fi

CMD=$1
if [[ ! -x $CMD ]]; then
    print -u2 -- "cannot execute ${CMD}"
    exit 1
fi

par=( "${(@)argv[2,-1]}" )
if [[ -z ${(M)par:#DISTFILE} ]]; then
    print -u2 -- "no DISTFILE among the arguments"
    exit 1
fi

results=( DISTFILE user )

exec 3<&1 4<&2
print "#bm timing $CMD $par"
for df in $distance_files;
do
    for i in {1..$REPEATS}; : $RANDOM
	UT=$( 2>&1 time (
	    for i in {1..$REPEATS};
	    do
		SEED=$RANDOM
		p=( "${(@)par/(#s)SEED(#e)/$SEED}" )
		p=( "${(@)p/(#s)DISTFILE(#e)/$df}" )
		print "#bm $i of $REPEATS timings for $df"
		print "#bm $CMD $p"
		$CMD "${(@)p}" || exit $?
	    done 1>&3 2>&4
	    )
	) || break
    print "#bm $UT\n"
    float -F3 AVGT=$(( ${UT%s} / REPEATS ))
    results=( $results ${df:t} $AVGT )
done

print
print "================================================================"
print "$CMD $par"
print "================================================================"
print -aC2 $results
